{
  description = "Kotiba application";

  inputs = {
    dream2nix.url = "github:nix-community/dream2nix";
    nixpkgs.follows = "dream2nix/nixpkgs";
  };

  outputs = {
    self,
    dream2nix,
    nixpkgs,
  }: let
    systems = ["x86_64-linux" "aarch64-linux" "aarch64-darwin"];
    eachSystem = nixpkgs.lib.genAttrs systems;
  in {
    packages = eachSystem (system: {
      default = dream2nix.lib.evalModules {
        packageSets.nixpkgs = nixpkgs.legacyPackages.${system};
        modules = [
          ./default.nix
          {
            paths.projectRoot = ./.;
            paths.projectRootFile = "flake.nix";
            paths.package = ./.;
          }
        ];
      };
    });

    devShells = eachSystem (system: let
      pkgs = nixpkgs.legacyPackages.${system};
      # hp = pkgs.haskell.packages.ghc912;
      hlib = pkgs.haskell.lib;
      hp = pkgs.haskell.packages."ghc912".override {
        overrides = self: super: {
          brick = hlib.dontCheck (hlib.doJailbreak super.brick);
          # cabal-install = hlib.dontCheck (hlib.doJailbreak super.cabal-install);
        };
      };
    in {
      default = pkgs.mkShell {
        nativeBuildInputs = [
          pkgs.cabal-install
          hp.ghc
          hp.haskell-language-server
          hp.fourmolu
          hp.hlint
          hp.ghcid
          hp.implicit-hie
          pkgs.haskellPackages.cabal-fmt
          pkgs.pkg-config
          pkgs.zlib
          pkgs.zlib.dev
          pkgs.bzip2
          pkgs.bzip2.dev
          pkgs.libzip
          pkgs.libpq
          pkgs.libpq.dev

          pkgs.nixd
          pkgs.statix
          pkgs.deadnix
          pkgs.treefmt
          pkgs.alejandra

          pkgs.jq
          pkgs.just
        ];

        shellHook = ''
          echo "Welcome to kotiba dev shell"
          export LD_LIBRARY_PATH=$LD_LIBRARY_PATH:${pkgs.postgresql}/lib
          export LD_LIBRARY_PATH=$LD_LIBRARY_PATH:${pkgs.libzip}/lib
          export LD_LIBRARY_PATH=$LD_LIBRARY_PATH:${pkgs.bzip2}/lib
          export LIBRARY_PATH=$LIBRARY_PATH:${pkgs.bzip2}/lib
          export NIX_LDFLAGS="$NIX_LDFLAGS -L${pkgs.bzip2}/lib"
        '';

        NIX_CONFIG = "extra-experimental-features = nix-command flakes";
      };
    });
    apps = eachSystem (system: let
      pkgs = nixpkgs.legacyPackages.${system};
      refresh = self.packages.${system}.default.config.lock.refresh;
    in {
      update-lock = {
        type = "app";
        program = "${pkgs.writeShellScript "update-lock" ''
          export PATH="${pkgs.git}/bin:${pkgs.cabal-install}/bin:$PATH"
          exec ${nixpkgs.lib.getExe refresh}
        ''}";
      };
    });
  };
}
